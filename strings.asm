*=$033C
STRINGS:
    LDA $2B
    STA $FB
    LDA $2C
    STA $FC
    LDY #$00
    STY $FF
PROGRAM_LOOP:
    LDA ($FB),Y
    INY
    STA $FD
    LDA ($FB),Y
    STA $FE
    ORA $FD
    BEQ STRINGS_EXIT
    LDY #$03
LINE_LOOP:
    INY
FIND_STRING:
    LDA ($FB),Y
    BEQ NEXT_LINE
    CMP #$22
    BEQ FOUND_STRING
    INY
    BNE FIND_STRING
FOUND_STRING
    LDA $C7
    EOR #$12
    STA $C7
    INY
STRING_LOOP:
    LDA #$01
    STA $D4
    STA $D8
    LDA ($FB),Y
    BEQ NEXT_LINE
    CMP #$22
    BEQ LINE_LOOP
    JSR $FFD2
    LDA $D3
    BEQ CHECK_PAUSE
    CMP #$28
    BNE NOPAGE
CHECK_PAUSE:
    INC $FF
    LDA $FF
    CMP #$18
    BNE NOPAGE
    LDA #$00
    STA $FF
    TYA
    PHA
PAUSE_PAGE:
    JSR $FFE4
    BEQ PAUSE_PAGE
    PLA
    TAY
NOPAGE:
    INY
    BNE STRING_LOOP
NEXT_LINE:
    LDA $FD
    STA $FB
    LDA $FE
    STA $FC
    LDY #$00
    BEQ PROGRAM_LOOP
STRINGS_EXIT:
    LDA #$0D
    JMP $FFD2
